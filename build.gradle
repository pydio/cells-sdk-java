plugins {
    // Enable test as a stand alone project
    id 'java'

    // Publish the generated resources as Maven Artifacts
    // See: https://docs.gradle.org/5.0/userguide/publishing_overview.html#publishing_overview
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

// Useful for Java only development
java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

def sdkVersion = '0.4.5'
ext {
    githubUrl = 'https://github.com/pydio/cells-sdk-java.git'
}

// Also publish sources and Javadoc
java {
    // Temporary disable javadoc, they are broken after updating to java 17 and gradle 8.1
    // withJavadocJar()
    withSourcesJar()
}

// Skip integration tests by default
test {
    if (System.properties['test.profile'] != 'integration') {
        exclude '**/integration/*'
    }
}

// Pre-create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "cdujeu"
            name "Charles du Jeu"
            email "charles@ajaxplorer.info"
        }
        developer {
            id "bsinou"
            name "Bruno Sinou"
            email "bruno.sinou@posteo.de"
        }
    }

    scm {
        url githubUrl
    }
}

publishing {
    publications {
        CellsSdk(MavenPublication) {
            from components.java
            groupId 'com.pydio.cells'
            artifactId 'cells-sdk-java'
            version = sdkVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Provide base java libraries to communicate with the Pydio Cells stack')
                root.appendNode('name', 'Cells SDK for Java')
                root.appendNode('url', 'https://pydio.com')
                root.children().last() + pomConfig
            }
        }
    }
}

jar {
    manifest {
        attributes('Specification-Title': 'Cells SDK for Java',
                'Specification-Version': '4.4',
                'Specification-Vendor': 'Abstrium SAS',
                'Implementation-Title': project.name,
                'Implementation-Vendor': 'Abstrium SAS',
                'Implementation-Version': sdkVersion)
    }
}

dependencies {

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation 'org.threeten:threetenbp:1.6.8'

    // Updating this cause IdToken decoding failure
    implementation 'commons-codec:commons-codec:1.16.1'

    // Used to store info as gson blobs in SQLite
    implementation 'com.google.code.gson:gson:2.10.1'
    // gsonfire library adds some more feature (like date de-serialization) to the basic GSON library
    // It is used by the swagger generated SDK code.
    implementation 'io.gsonfire:gson-fire:1.9.0'

    // Necessary when developing / building with a JDK 11
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    // https://mvnrepository.com/artifact/jakarta.ws.rs/jakarta.ws.rs-api
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    // Necessary for swagger (generation of the openapi package from Cells spec)
    implementation 'io.swagger:swagger-annotations:1.6.13'
    // Add the @Nullable annotation
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    // implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'


    // JUnit test framework
    testImplementation 'junit:junit:4.13.2'

    // S3 transport is configured in calling layer and use a specific either Java or Android implementation.
    // See cells-android-client or cells-java-client repos.
}

// Helper tasks to debug

// Uncomment this to get more messages during build during code cleaning sprints.
// tasks.withType(JavaCompile) {
//    options.compilerArgs << '-Xlint:unchecked'
//    options.deprecation = true
// }

tasks.register('printSourceSetInformation') {
    doLast {
        sourceSets.each { srcSet ->
            println "[" + srcSet.name + "]"
            print "-->Source directories: " + srcSet.allJava.srcDirs + "\n"
            print "-->Output directories: " + srcSet.output.classesDirs.files + "\n"
            print "-->Compile classpath:\n"
            srcSet.compileClasspath.files.each {
                print "  " + it.path + "\n"
            }
            println ""
        }
    }
}
