// This gradle file is used when the sdk java is opened as a sub-module of the cells-android-app repo.
// See the sibling "build-standalone.gradle" file if you are build thinge SDK Java as a stand alone module.

plugins {
    // Build java project
    id 'java-library'

    // Publish the generated resources as Maven Artifacts
    // See: https://docs.gradle.org/5.0/userguide/publishing_overview.html#publishing_overview
    id 'maven-publish'
}

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

def vendorName = "Abstrium SAS"
def sdkJavaRepoURL = 'https://github.com/pydio/cells-sdk-java.git'
def artifactID = 'cells-sdk-java'
def sdkJavaSpecVersion = '0.4.6'
def sdkJavaVersion = ownVersions['java.sdk']

// Also publish sources and Javadoc
java {
    // javadoc is disabled: they are broken after updating to java 17 and gradle 8.1
    // withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

// Skip integration tests by default
test {
    if (System.properties['test.profile'] != 'integration') {
        exclude '**/integration/*'
    }
}

publishing {
    publications {
        CellsSdk(MavenPublication) {
            from components.java
            groupId 'com.pydio.cells'
            artifactId 'cells-sdk-java'
            version = sdkJavaVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Provide base java libraries to communicate with the Pydio Cells stack')
                root.appendNode('name', 'Cells SDK for Java')
                root.appendNode('url', 'https://pydio.com')

                root.children().last() + rootProject.getBasePom(sdkJavaRepoURL)
            }
        }
    }
}

jar {
    manifest {
        attributes('Specification-Title': 'Cells SDK for Java',
                'Specification-Version': sdkJavaSpecVersion,
                'Specification-Vendor': vendorName,
                'Implementation-Title': artifactID,
                'Implementation-Vendor': vendorName,
                'Implementation-Version': sdkJavaVersion)
    }
}

dependencies {
    implementation libs.commons.codec
    implementation libs.threetenbp
    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation libs.google.gson
    implementation libs.gson.fire
    implementation libs.jackson.databind

    // Necessary when developing / building with a JDK 11
    implementation libs.javax.annotation.api
    implementation libs.swagger.annotations
    implementation libs.jsr305
    implementation libs.jakarta.ws.rs.api

    // JUnit test framework
    testImplementation libs.junit
}
