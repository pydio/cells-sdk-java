plugins {
    // Build java project
    id 'java-library'

    // Publish the generated resources as Maven Artifacts
    // See: https://docs.gradle.org/5.0/userguide/publishing_overview.html#publishing_overview
    id 'maven-publish'
}

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

def vendorName = "Abstrium SAS"
def sdkJavaRepoURL = 'https://github.com/pydio/cells-sdk-java.git'
def artifactID = 'cells-sdk-java'
def sdkJavaSpecVersion = '0.4.6'
def sdkJavaVersion = ownVersions['java.sdk']

// Also publish sources and Javadoc
java {
    // javadoc is disabled: they are broken after updating to java 17 and gradle 8.1
    // withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

// Skip integration tests by default
test {
    if (System.properties['test.profile'] != 'integration') {
        exclude '**/integration/*'
    }
}

publishing {
    publications {
        CellsSdk(MavenPublication) {
            from components.java
            groupId 'com.pydio.cells'
            artifactId 'cells-sdk-java'
            version = sdkJavaVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Provide base java libraries to communicate with the Pydio Cells stack')
                root.appendNode('name', 'Cells SDK for Java')
                root.appendNode('url', 'https://pydio.com')

                root.children().last() + rootProject.getBasePom(sdkJavaRepoURL)
            }
        }
    }
}

jar {
    manifest {
        attributes('Specification-Title': 'Cells SDK for Java',
                'Specification-Version': sdkJavaSpecVersion,
                'Specification-Vendor': vendorName,
                'Implementation-Title': artifactID,
                'Implementation-Vendor': vendorName,
                'Implementation-Version': sdkJavaVersion)
    }
}

dependencies {

    // Set of carefully chosen third-party libraries. See sibling "build.gradle" file.
    // Do not update if you do not *really* know what you are doing.

    implementation 'commons-codec:commons-codec:1.17.0'
    implementation 'org.threeten:threetenbp:1.6.8'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.gsonfire:gson-fire:1.9.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'

    // Necessary when developing / building with a JDK 11
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.swagger:swagger-annotations:1.6.13'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'

    // JUnit test framework
    testImplementation 'junit:junit:4.13.2'
}
