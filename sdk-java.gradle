plugins {
    // Build java project
    id 'java-library'

    // Publish the generated resources as Maven Artifacts
    // See: https://docs.gradle.org/5.0/userguide/publishing_overview.html#publishing_overview
    id 'maven-publish'
}


java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

def vendorName = "Abstrium SAS"
def sdkJavaRepoURL = 'https://github.com/pydio/cells-sdk-java.git'
def artifactID = 'cells-sdk-java'
def sdkJavaSpecVersion = '0.2'
def sdkJavaVersion = ownVersions['java.sdk']

// Also publish sources and Javadoc
java {
    withJavadocJar()
    withSourcesJar()
}

// Skip integration tests by default
test {
    if (System.properties['test.profile'] != 'integration') {
        exclude '**/integration/*'
    }
}

publishing {
    publications {
        CellsSdk(MavenPublication) {
            from components.java
            groupId 'com.pydio.cells'
            artifactId 'cells-sdk-java'
            version = sdkJavaVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Provide base java libraries to communicate with the Pydio Cells stack')
                root.appendNode('name', 'Cells SDK for Java')
                root.appendNode('url', 'https://pydio.com')

                root.children().last() + rootProject.getBasePom(sdkJavaRepoURL)
            }
        }
    }
}

jar {
    manifest {
        attributes('Specification-Title': 'Cells SDK for Java',
                'Specification-Version': sdkJavaSpecVersion,
                'Specification-Vendor': vendorName,
                'Implementation-Title': artifactID,
                'Implementation-Vendor': vendorName,
                'Implementation-Version': sdkJavaVersion)
    }
}

dependencies {

    // Set of carefully chosen third-party libraries
    // Do not update lightly: see build.gradle sibling file.

    implementation 'io.swagger:swagger-annotations:1.6.3'
    implementation 'com.metaparadigm:json-rpc:1.0'
    
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation 'org.threeten:threetenbp:1.5.2'

    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okhttp:logging-interceptor:2.7.5'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'commons-codec:commons-codec:1.15'
    // Enable the use of @Nullable and @NonNull in method parameters for cleaner code
    implementation 'org.jetbrains:annotations:21.0.1'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13.2'
    // When running with android these are provided by the platform
    testImplementation 'com.amazonaws:aws-java-sdk-core:1.11.1030'
    testImplementation 'com.amazonaws:aws-java-sdk-s3:1.11.1030'

}
